import {
  add as (+),
  sub as (-),
  land as (&),
  gt as (>),
  eq as (==),
} from "int32"
import W4 from "wasm4"
import { tagSimpleNumber } from "runtime/dataStructures"
import WasmI32 from "runtime/unsafe/wasmi32"

let smiley = b"\xc3\x81\x24\x24\x00\x24\x99\xc3"

let itsDangerous = "IT'S DANGEROUS\n TO GO ALONE!"

let mut x = 76l
let mut y = 76l

let mut frame = 0l

export let update = () => {
  if (frame == 108l) {
    frame = 0l
  }

  // Load the gamepad
  let gamepad = W4.gamepad1()

  // Move the player location
  if ((gamepad & W4._BUTTON_LEFT) > 0l) {
    x -= 1l
  }
  if ((gamepad & W4._BUTTON_RIGHT) > 0l) {
    x += 1l
  }
  if ((gamepad & W4._BUTTON_UP) > 0l) {
    y -= 1l
  }
  if ((gamepad & W4._BUTTON_DOWN) > 0l) {
    y += 1l
  }

  // Swap to color 2
  W4.drawColors(2l)
  // Write the text
  W4.text(itsDangerous, 24l, 50l)

  // Swap to color 3
  W4.drawColors(3l)
  // Draw the sprite
  W4.blit(smiley, x, y, 8l, 8l, W4._BLIT_1BPP)

  frame += 1l
}
